<TITLE>NetCDF_Put</TITLE> 
<body bgcolor="#f9ff91"> 

<blockquote>
<br>
<h3>Saving data in a NetCDF file</h3> 
 
  The IDLUSR routine <b>ncdf_put</b> is one of the IDLUSR NetCDF access routines,
  the others are  <b>ncdf_get</b> and  <b>ncdf_list</b>.  The normal IDL access
  routines are listed at the end of this page (33 of them).  The IDLUSR routines do
  not (yet) do everything that can be done with the IDL routines but should be useful
  for quick and simple data output and input.  Currently the three IDLUSR NetCDF
  routines do not deal with Groups.
  <p>
  The NetCDF format is fairly complex which explains the large number of IDL routines.  The three
  simple access routines trade some of the IDL routines for a bit more complexity in the input
  to the IDLUSR <b>ncdf_put</b> routine.  Items to be written to a NetCDF file are given to this
  routine using the IDL <i>hash</i> data type.  A <i>hash</i> can do many of the functions of a
  <i>structure</i> but the keys (instead of structure tags) are case sensitive and the values may
  be changed without rebuilding the hash.
  <p>
  Two main kinds of item may be written to a NetCDF file, <i>Global Attributes</i> and <i>Variables</i>.
  Global attributes are given as a hash containing keys (the attribute name) and values, any number of
  such pairs.  Variables are also given with a hash of similar pairs, a key and a value, but the value
  is another hash containing the actual value, the dimension names, dimension sizes, and
  attributes for the variable.  The variable attributes is a hash of pairs, keys and values.
  So the variables are actually given using a triply nested hash (similar to a nested structure).
  A working example will be given below of writing data to a NetCDF file using <b>ncdf_put</b>.
  <p>
  Attributes for a variable is optional.  So is the dimension information, if none is given
  defaults are constructed from the variable name and the array dimensions.  The dimension
  information is always constructed for text strings (scalars or arrays) so should not be given.
  Attributes for a text string variable are optional.
  <p>
  The two main inputs to <b>ncdf_put</b> are global attributes and variables.
  Global attributes are easy to set up in a hash, see Example 1 below.  The global attributes
  hash is sent to <b>ncdf_put</b> through the GATT keyword.
  Variables are a bit more involved.
  First set up the attributes for each variable in a hash.  This is simple, like setting up the
  global attributes.  Next for each variable set up the value hash which contains the actual value, any
  dimensions info, and the attributes hash, if any, for the variable.  Finally pack all the variables
  into the variable hash which is a set of pairs, one for each variable, of the variable name as the
  key and the variable value hashes as the values for the keys.  This is what is sent to
  <b>ncdf_put</b> through the VARIABLES keyword.
  <p>

  <h3>ncdf_put built in help</h3>
  <pre>
 ncdf_put,/help
 Write given data to a NetCDF file.
 ncdf_put, file
   file = Name of NetCDF file to write or add to.  in
 Keywords:
   GATT=gatt  Global attributes in a hash.
   VARIABLES=var  Data variables in a hash.
   /NEW Start a new NetCDF file if file exists.
   ERROR=err Error flag, 0=ok.
   ERR_TXT=err_txt Error text.
   /DETAILS gives details of inputs to this routine:
     ncdf_put,'',/details
</pre>

  <h3>ncdf_put details</h3>
  <pre>
        ncdf_put,/details

        A NetCDF file contains variables and optional global attributes.

        A NetCDF variable has a name, a value, and some number of attributes.
          The variable name is case sensitive.
          The value be an array and will have dimensions which have names and sizes.
          The attributes have names and values.

        Variable Hash layout
          NetCDF variable names are case sensitive so a hash is used to contain them.

          variables = hash(name1, value1, name2, value2, ...)
            name1 is the name of variable 1 (and the hash key to access it).
            value1 is a hash with:
              value: value_of_variable,
              dim_names:  String array: [nm1, nm2, ...],
              dim_sizes:  Long array: [sz1, sz2, ...],
              attributes: Hash: hash(att_tag1, att_value1, att_tag2, att_value2, ...)
            Note: dim_names, dim_sizes, and attributes are optional.  If dimensions
            are not given the dimension names will default to name1_dim1, name1_dim2, ...
            and the sizes will be set from the array size.

         Global Attributes
               gatt = hash(gatt_tag1, gatt_value1, gatt_tag2, gatt_value2, ...)
</pre>

<h3>The IDL hash data type</h3>

A hash (or hash table) is a data type and in IDL is set up using the built in
IDL function called <b>hash</b>.  A hash is built by giving the <b>hash</b> function
pairs of keys and values.  A key can be a scalar string or a number.  A value can be
a scalar or array of any IDL data type.  The syntax is:
<pre>
        h = hash(key1, value1, key2, value2, ...)
</pre>
There are some variations on this syntax, see the IDL help for hash.

<h3>Example 1: Create a new NetCDF file</h3>
<pre>
    ;---  Set up 3 array variables, x, y, and z for ncdf_put  ---
    z = bytscl(makez(500,300))                                   ; Variable z.
    makenxy,10,20,500,30,40,300,x,y                              ; Variables x and y.

    ;---  Set up 2 scalar values  ---
    a = 111                                                      ; Variable a.
    b = 222                                                      ; Variable b.

    ;---  Set up global attributes  ---
    gatt = hash('desc','Test of ncdf_put','N_arrays',3)          ; Global attributes.

    ;---  Set up attributes of variables  ---
    z_att = hash('desc','Test image','min',min(z),'max',max(z))  ; Attributes of variable z.
    x_att = hash('units','x_pixels','min',min(x),'max',max(x))   ; Attributes of variable x.
    y_att = hash('units','y_pixels','min',min(y),'max',max(y))   ; Attributes of variable y.
    a_att = hash('desc','Test scalar variable 1','units','none') ; Attributes of variable a.
    b_att = hash('desc','Test scalar variable 2','Units','None') ; Attributes of variable b.

    ;---  Set up variable values  ---
    z_val = hash('value',z,'dim_names',['East-West','North-South'],'dim_sizes',[500,300],'attributes',z_att)
    x_val = hash('value',x,'dim_names',['East-West','North-South'],'dim_sizes',[500,300],'attributes',x_att)
    y_val = hash('value',y,'dim_names',['East-West','North-South'],'dim_sizes',[500,300],'attributes',y_att)
    a_val = hash('value',a,'attributes',a_att)
    b_val = hash('value',b,'attributes',b_att)

    ;---  Set up variables hash  ---
    var = hash('z',z_val,'x',x_val,'y',y_val,'a',a_val,'b',b_val)

    ;---  Now actually write everything to a NetCDF file  ---
    ncdf_put,'first_ncdf.nc',gatt=gatt,var=var

</pre>


<h3>Example 2: Add to the NetCDF file created in Example 1</h3>
<pre>
    ;---  Set up new variables  ---
    ;---  New array, old dims  ---
    x2 = makez(500,300)
    x2_att = hash('Att_1','AVal_1','Att_2','AVal_2')
    x2_val = hash('value',x2,'dim_names',['East-West','North-South'],'dim_sizes',[500,300],'attributes',x2_att)

    ;---  New array, old dims  ---
    y2 = makez(500,300)
    y2_att = hash('Att_1','AVal_1','Att_2','AVal_2')
    y2_val = hash('value',y2,'dim_names',['East-West','North-South'],'dim_sizes',[500,300],'attributes',y2_att)

    ;---  New array, new dims  ---
    w = makez(200,400)
    w_att = hash('Desc','New array, new dims')
    w_val = hash('value',w,'dim_names',['Hor','Ver'],'dim_sizes',[200,400],'attributes',w_att)

    ;---  New array, def dims  ---
    u = makez(333,555)
    u_att = hash('Desc','New array, def dims')
    u_val = hash('value',u,'attributes',u_att)

    ;---  New scalar  ---
    c = 333
    c_att = hash('desc','New scalar variable','units','NONE')
    c_val = hash('value',c,'attributes',c_att)

    ;---  New scalar  ---
    d = 444
    d_att = hash('desc','New scalar variable','units','NONE')
    d_val = hash('value',d,'attributes',d_att)

    ;---  Set up variables hash  ---
    var = hash('x2',x2_val,'y2',y2_val,'w',w_val,'u',u_val,'c',c_val,'d',d_val)

    ;---  Now actually add everything to a NetCDF file  ---
    ncdf_put,'first_ncdf.nc',var=var,/verb
    

</pre>



  <br><br><br>
<p>
<hr> 
The normal IDL NetCDF access routines are the following:
<pre>
	NCDF_VARDEF
	NCDF_VARPUT
	NCDF_ATTINQ
	NCDF_GROUPNAME
	NCDF_VARGET
	NCDF_GROUPDEF
	NCDF_VARGET1
	NCDF_CONTROL
	NCDF_ATTCOPY
	NCDF_ATTPUT
	NCDF_ATTGET
	NCDF_ATTDEL
	NCDF_FULLGROUPNAME
	NCDF_ATTNAME
	NCDF_CREATE
	NCDF_GROUPPARENT
	NCDF_GROUPSINQ
	NCDF_NCIDINQ
	NCDF_VARINQ
	NCDF_DIMIDSINQ
	NCDF_VARIDSINQ
	NCDF_UNLIMDIMSINQ
	NCDF_ATTRENAME
	NCDF_DIMINQ
	NCDF_DIMRENAME
	NCDF_VARRENAME
	NCDF_INQUIRE
	NCDF_DIMDEF
	NCDF_DIMID
	NCDF_VARID
	NCDF_EXISTS
	NCDF_CLOSE
	NCDF_OPEN
</pre>

<hr> 
 
</blockquote>
</body> 

